alias PATHeat d0 #or fuel!
alias VPHeat d1 #or fuel!
alias PATCoolant d2
alias VPCoolant d3
alias PATWaste d4
alias VPWaste d5
define V 1000
define PumpMax 10 #input pipe volume!
define FuelScale 3 #When running direct fuel input: big number => slow; small => fast
define AdvanceFurnace 545937711
alias IsFuel r2
alias MainPointer r3
alias SubPointer r4
alias TempSP r5
alias PresSP r6
alias th r7
alias tc r8
alias tw r9
alias TempCur r10
alias PresCur r11
alias vh r12
alias vc r13
alias vo r14
alias vw r15
Start:
yield
l r0 db Setting
trunc PresSP r0
sub r0 r0 PresSP
mul TempSP r0 10000
l r0 PATHeat RatioOxygen
l r1 PATHeat RatioNitrousOxide
sgt th r1 r0
max r0 r0 r1
l r1 PATHeat RatioVolatiles
min r0 r0 r1
sgt IsFuel r0 0.1
select th th 6000 3000
l r1 PATHeat Temperature
select th IsFuel th r1
l tc PATCoolant Temperature
l tw PATWaste Temperature
lb TempCur AdvanceFurnace Temperature Average
select TempCur TempCur TempCur 1
lb PresCur AdvanceFurnace Pressure Average
select PresCur PresCur PresCur 1
move MainPointer 8
jal InputTempCalc
max vc r0 0
move MainPointer 7
jal InputTempCalc
max vh r0 0
sub vo PresSP PresCur #vo = pv_s
mul vo vo V
sub vo vo vc
sub vo vo vh
max vo vo 0
move SubPointer 9
jal InputPressureCalc
blez r0 CoolantCalc #ToA
max vh r0 vh
jal Flip
max vw r0 0
j voCalc #ToB
CoolantCalc:
move MainPointer 8 #A
jal InputPressureCalc
max vc r0 vc
jal Flip
max vw r0 0
voCalc:
add vo vc vh #B
add vo vo vw
mul r0 PresCur V
add r0 r0 vo
sub r1 PresCur PresSP
mul r1 r1 V
add r1 r1 vo
mul r1 r1 V
div vo r1 r0
l r0 PATHeat Pressure
select r1 IsFuel FuelScale 1
mul r0 r0 r1
div vh vh r0
l r0 PATCoolant Pressure
div vc vc r0
l r0 PATWaste Pressure
div vw vw r0
sb AdvanceFurnace SettingOutput vo
sb AdvanceFurnace SettingInput PumpMax
div r0 PumpMax r1
min vh vh r0
min vc vc PumpMax
min vw vw PumpMax
s VPHeat Setting vh
s VPCoolant Setting vc
s VPWaste Setting vw
lb r0 AdvanceFurnace On Minimum
s VPHeat On r0
s VPCoolant On r0
s VPWaste On r0
sgt r0 vh 0.1
lb r1 AdvanceFurnace Combustion Minimum
and r0 r0 IsFuel
sgt r0 r0 r1
sb AdvanceFurnace Activate r0
j Start
InputTempCalc: #r3
sub r0 TempSP rr3
mul r0 r0 TempCur
sub r1 TempCur TempSP
mul r1 r1 V
mul r1 r1 PresCur
mul r1 r1 rr3
div r0 r1 r0
j ra
Flip:
move r0 MainPointer
move MainPointer SubPointer
move SubPointer r0
InputPressureCalc: #r3 = main
sub r0 rr3 rr4
mul r0 r0 TempSP
sub r1 TempSP rr4
mul r1 r1 rr3
mul r1 r1 vo
div r0 r1 r0
j ra