alias DTPoweredVent d0
alias NTPoweredVent d1
alias DTTank d2
alias NTTank d3
alias StirlingInputVP d4
alias GasSensor d5

define DTMinTemperature 900 # K | Min temperature for the daytime vent
define NTMaxTemperature 425 # K | Max temperature for the nighttime vent
define InputPressureMax 10000 # kPa | Max pressure for the stirling engine input regulator. Warning! Pressure over 11 mPa will instantly destroy the engine!
define DTMaxPressure 50000 # kPa | Max pressure for the daytime vent
define NTMaxPressure 6000 # kPa | Max pressure for the nighttime vent, constrained by volatile liquid temperature
define ExternalMaxTemp 1500 # K | Max external temperature on Vulcan

alias ExternalTemperature r15 
alias DTTankPressure r14
alias NTTankPressure r13
alias StirlingInputPower r12
alias DTVentPower r11
alias NTVentPower r10

init:
s DTPoweredVent Mode Vent.Inward
s DTPoweredVent On 0 # Don't input more air if setup fails
s DTPoweredVent Lock 0

s NTPoweredVent Mode Vent.Inward
s NTPoweredVent On 0 # Don't input more air if setup fails
s NTPoweredVent Lock 0

l r0 StirlingInputVP Maximum
s StirlingInputVP Setting r0
s StirlingInputVP On 1
s StirlingInputVP Lock 0

s $CF3B On 1 # SE Input PA
s $CF3B Lock 1 # SE Input PA

s $CC6D Mode InputPressureMax

s $C979 Mode DisplayMode.Kelvin
s $C97E Mode DisplayMode.Kelvin
s $C9A4 Mode DisplayMode.Kelvin
s $C9A6 Mode DisplayMode.Kelvin

start:
    yield
    seDisplay:
    # SE1 $B097
    l r0 $B097 Pressure
    l r1 $B097 Temperature
    s $C976 Setting r0
    s $C979 Setting r1
    l r1 $B097 On
    s $C972 On r1

    inputControl:
    l r0 $CF3B Pressure # SE Input PA
    s $CC75 Setting r0 # SE Input pressure display
    l r1 $CC6D Setting # SE Input pressure dial
    slt StirlingInputPower r0 r1

    daytimeControl:
    l ExternalTemperature GasSensor Temperature
    l DTTankPressure DTTank Pressure
    l r0 DTTank Temperature

    s $CB6E Setting DTTankPressure # DT tank pressure display
    s $CB70 Setting r0 # DT tank temperature display
    
    l r0 $CB75 Setting # DT tank pressure dial
    l r1 $CB77 Setting # DT tank temperature dial

    slt r0 DTTankPressure r0
    s $CBBF On r0 # DT tank pressure LED
    sgt r1 ExternalTemperature r1
    s $CBC4 On r1 # DT tank temperature LED
    and DTVentPower r0 r1
    l r0 DTPoweredVent On
    s $CBC6 On r0 # DT overall status LED

    nighttimeControl:
    l NTTankPressure NTTank Pressure
    l r0 NTTank Temperature

    s $CB6F Setting NTTankPressure # NT tank pressure display
    s $CB74 Setting r0 # NT tank temperature display

    l r0 $CB76 Setting # NT tank pressure dial
    l r1 $CB78 Setting # NT tank temperature dial

    slt r0 NTTankPressure r0
    s $CBC0 On r0 # NT tank pressure LED
    slt r1 ExternalTemperature r1
    s $CBC5 On r1 # NT tank temperature LED
    and NTVentPower r0 r1
    l r0 NTPoweredVent On
    s $CBC7 On r0 # DT overall status LED

    checkShutdown:
    l r0 $CC0A Activate # Emergency shutoff button
    l r1 $CC0B Setting # Main breaker
    or r2 r0 r1
    s $CC0B Open r2 # Open breaker if emergency button is pressed
    bnez r1 shutdown # If breaker is open, initiate shutdown

    write:
    s DTPoweredVent On DTVentPower
    s NTPoweredVent On NTVentPower
    s StirlingInputVP On StirlingInputPower
j start

shutdown:
s DTPoweredVent On 0 # Stop inputting air
s NTPoweredVent On 0 # Stop inputting air
s StirlingInputVP On 0
sb HASH("StructureStirlingEngine") On 0
j start