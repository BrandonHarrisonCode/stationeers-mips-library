alias counter r15
alias printer r14
alias stacker r13
alias stackLength r12
alias lasthash r11
alias currenthash r10
alias setting r9
alias deviceExists r8

define INGOTSIZE 500
define INTERNALSLOT 2

init:
lb lasthash HASH("StructureAutolathe") RecipeHash Minimum
push lasthash
push HASH("StructureAutolathe")
push HASH("Autolathe Stacker")
sb HASH("StructureAutolathe") ClearMemory 1

lb lasthash HASH("StructureElectronicsPrinter") RecipeHash Minimum
push lasthash
push HASH("StructureElectronicsPrinter")
push HASH("Electronics Printer Stacker")
sb HASH("StructureElectronicsPrinter") ClearMemory 1

lb lasthash HASH("StructureHydraulicPipeBender") RecipeHash Minimum
push lasthash
push HASH("StructureHydraulicPipeBender")
push HASH("Hydraulic Pipe Bender Stacker")
sb HASH("StructureHydraulicPipeBender") ClearMemory 1

lb lasthash HASH("StructureToolManufactory") RecipeHash Minimum
push lasthash
push HASH("StructureToolManufactory")
push HASH("Tool Manufactory Stacker")
sb HASH("StructureToolManufactory") ClearMemory 1

move stackLength sp

start:
	select sp sp sp stackLength # Reset stack pointer if it reaches 0
	pop stacker
	pop printer
	
	lb deviceExists printer PrefabHash Maximum
	beqz deviceExists skip # Continue if printer does not exist
	lbn deviceExists HASH("StructureStacker") stacker PrefabHash Maximum
	beqz deviceExists skip # Continue if stacker does not exist
	
	# Turn on stacker if printer is on
	lb setting printer On Minimum
	sbn HASH("StructureStacker") stacker On setting
	# Continue if the printer is off
	beqz setting skip
	
	lb r0 printer Open Minimum
	lbn r1 HASH("StructureStacker") stacker Setting Minimum
	
	# If printer is open (ejecting ingots), set stacker to 500
	select r2 r0 INGOTSIZE r1 
	sbn HASH("StructureStacker") stacker Setting r2
	
	# Constantly clear stacker if ejecting
	seq r3 r2 INGOTSIZE
	lbns r4 HASH("StructureStacker") stacker INTERNALSLOT Occupied Minimum # Only if something is in the stacker 
	and r3 r3 r4
	sbn HASH("StructureStacker") stacker Activate r3
	
	# If printer is closed but stacker is set to 500, set it to 1
	seq r3 r1 INGOTSIZE
	seqz r4 r0
	and r3 r3 r4 # 1 if stacker should be reset, 0 if nothing should happen
	select r2 r3 1 r2
	sbn HASH("StructureStacker") stacker Setting r2
	
	# Reset memory if we change the recipe
	lb currenthash printer RecipeHash Minimum
	pop lasthash
	push currenthash
	sne setting lasthash currenthash
	sb printer ClearMemory setting
	sbn HASH("StructureStacker") stacker Activate setting

	# Check if we're done printing
	lb r0 printer ExportCount Minimum
	lbn r1 HASH("StructureStacker") stacker Setting Minimum
	blt r0 r1 end # If still more to printer, continue

	# Export count has been reached, stop the printer
	sb printer Activate 0
	sb printer ClearMemory 1
	
	end:
	yield
	skip:
	sub sp sp 1 # Accounting for lasthash/currenthash
j start