alias counter r15
alias printer r14
alias stacker r13
alias lasthash r0
alias currenthash r1
alias setting r2
alias deviceExists r3

define MAXDEVICES 4 # The total number of printers that exist in the game / defined in the init section below
define INGOTSIZE 500
define INTERNALSLOT 2

init:
push HASH("StructureAutolathe")
push HASH("Autolathe Stacker")
lb lasthash HASH("StructureAutolathe") RecipeHash Minimum
push lasthash
sb HASH("StructureAutolathe") ClearMemory 1

push HASH("StructureElectronicsPrinter")
push HASH("Electronics Printer Stacker")
lb lasthash HASH("StructureElectronicsPrinter") RecipeHash Minimum
push lasthash
sb HASH("StructureElectronicsPrinter") ClearMemory 1

push HASH("StructureHydraulicPipeBender")
push HASH("Hydraulic Pipe Bender Stacker")
lb lasthash HASH("StructureHydraulicPipeBender") RecipeHash Minimum
push lasthash
sb HASH("StructureHydraulicPipeBender") ClearMemory 1

push HASH("StructureToolManufactory")
push HASH("Tool Manufactory Stacker")
lb lasthash HASH("StructureToolManufactory") RecipeHash Minimum
push lasthash
sb HASH("StructureToolManufactory") ClearMemory 1

start:
	move counter 0
	move sp 1 # Stacks start at 1
	
	# Loop through all pairs of printers and stackers and give a tick to handle power changes before handling logic changes
	logic:
	peek printer
	add sp sp 1
	peek stacker
	add sp sp 1
	
	lb deviceExists printer PrefabHash Maximum
	beqz deviceExists logicInc # Continue if printer does not exist
	lbn deviceExists HASH("StructureStacker") stacker PrefabHash Maximum
	beqz deviceExists logicInc # Continue if stacker does not exist
	
	# Turn on stacker if printer is on
	lb setting printer On Minimum
	sbn HASH("StructureStacker") stacker On setting
	
	lb r0 printer Open Minimum
	lbn r1 HASH("StructureStacker") stacker Setting Minimum
	
	# If printer is open (ejecting ingots), set stacker to 500
	select r2 r0 INGOTSIZE r1
	sbn HASH("StructureStacker") stacker Setting r2
	
	# Constantly clear stacker if ejecting
	seq r3 r2 INGOTSIZE
	lbns r4 HASH("StructureStacker") stacker INTERNALSLOT Occupied Minimum # Only if something is in the stacker 
	and r3 r3 r4
	sbn HASH("StructureStacker") stacker Activate r3
	
	# If printer is closed but stacker is set to 500, set it to 1
	seq r3 r1 INGOTSIZE
	seqz r4 r0
	and r3 r3 r4 # 1 if stacker should be reset, 0 if nothing should happen
	select r2 r3 1 r2
	sbn HASH("StructureStacker") stacker Setting r2
	
	lb currenthash printer RecipeHash Minimum
	pop lasthash
	push currenthash
	sne setting lasthash currenthash
	sb printer ClearMemory setting
	sbn HASH("StructureStacker") stacker Activate setting
	
	lbn r0 HASH("StructureStacker") stacker Setting Minimum
	lb r1 printer ExportCount Minimum
	
	blt r1 r0 logicInc # If still more to printer, continue
	
	# Export count has been reached, stop the printer
	sb printer Activate 0
	sb printer ClearMemory 1
	
	# Increment device counter
	logicInc:
	add sp sp 1
	add counter counter 1
	mod r0 counter 
	yield
	ble counter MAXDEVICES logic
j start