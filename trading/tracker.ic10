alias Dish d0
alias StatusLED d1

alias traderID r15
alias signalStrength r14
alias interrogationProgress r13
alias minimumWatts r12
alias wattsReachingContact r11
alias setWatts r10
alias horizontalSearchDirection r9
alias verticalSearchDirection r8
alias newSignalStrength r7

define HorizontalSearchStep 30
define VerticalSearchStep 15
define SearchDelay 5 # The amount of time to attempt to resolve signals during the search phase before abandoning
define TrackFactor 4 # The divisor for the degree offset to determine how much to move the dish, higher numbers are more precise but take longer

init:
s Dish On 1
move horizontalSearchDirection 1
move verticalSearchDirection 1

search:
    s StatusLED On 1
    s StatusLED Color Color.White
    s Dish BestContactFilter -1 # Investigate all traders
    sleep SearchDelay
    l traderID Dish SignalID
    l signalStrength Dish SignalStrength 
    sgtz r0 signalStrength # Is there a signal
    l interrogationProgress Dish InterrogationProgress
    slez r1 interrogationProgress # Have we already tried to resolve this signal
    and r0 r0 r1
    bgtz r0 track # Contact found
    # Contact not found
    l r0 Dish Horizontal
    add r0 r0 HorizontalSearchStep
    s Dish Horizontal r0
    l r1 Dish Vertical
    brle r0 360 2 # If we have gone full circle, move the VerticalStepSize 
    add r1 r1 VerticalSearchStep
    brle r1 90 2 # If we have scanned the full vertical range, reset vertical 
    move r1 VerticalSearchStep
    s Dish Vertical r1
    waitForMove:
    yield
    l r0 Dish Idle
    beqz r0 waitForMove
j search

track:
s Dish BestContactFilter traderID # Ignore all other signals besides the one we found
s StatusLED On 1
l minimumWatts Dish MinimumWattsToContact
l wattsReachingContact Dish WattsReachingContact
blt minimumWatts wattsReachingContact interrogate # Can begin interrogation
l setWatts Dish Setting
bgt minimumWatts setWatts failure # There will never be enough watts to contact this trader
trackHorizontal:
    s StatusLED Color Color.Orange
    l r0 Dish Horizontal
    div r1 signalStrength TrackFactor
    mul r1 r1 horizontalSearchDirection
    add r0 r0 r1
    s Dish Horizontal r0

    jal evaluateTracking
    beq r0 1 trackVertical # We have improved the signal horizontally, now track vertically
    mul horizontalSearchDirection horizontalSearchDirection -1 # We moved in the wrong direction
j track

trackVertical:
    s StatusLED Color Color.Yellow
    l r0 Dish Vertical
    div r1 signalStrength TrackFactor
    mul r1 r1 verticalSearchDirection
    add r0 r0 r1
    max r0 r0 5 # Clamp vertical to 5-90, the horizontal signal will never improve if vertical is 0
    s Dish Vertical r0

    jal evaluateTracking
    beq r0 1 track # We have improved the signal vertically, now track horizontally
    mul verticalSearchDirection verticalSearchDirection -1 # We moved in the wrong direction
j trackVertical

evaluateTracking:
    yield
    l r0 Dish InterrogationProgress
    bgtz r0 search # We have already made contact with this signal
    l r0 Dish SignalID
    beq r0 -1 failure # The trader has left before we tracked them
    l newSignalStrength Dish SignalStrength
    beq newSignalStrength -1 evaluateTracking # Wait for the signal to appear
    slt r0 newSignalStrength signalStrength # Is the new signal stronger than the old one. Use lt rather than le since if the signal didn't improve we either skipped past it perfectly or we are at the extremes of motion
    move signalStrength newSignalStrength
j ra

interrogate:
s StatusLED Color Color.Green
s Dish Activate 1 

waitForInterrogation:
yield
l r0 StatusLED On
seqz r0 r0
s StatusLED On r0 # Blink LED
l r0 Dish SignalID
beq r0 -1 failure # The trader has left before we interrogated them
l r0 Dish InterrogationProgress
bne r0 1 waitForInterrogation # Wait for the interrogation to complete

success:
    yield
    s StatusLED On 1
    s StatusLED Color Color.Blue
    l r0 db Setting
    add r0 r0 1
    s db Setting r0 # Count successful interrogations
    sleep 1
j search
failure:
    yield
    s StatusLED Color Color.Red
    s StatusLED On 1
    sleep 1
j search